plugins {
    id 'forgified-fabric-loom' version '0.6-SNAPSHOT'
    id 'maven-publish'
    id 'com.matthewprenger.cursegradle' version '1.4.0'
}

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

apply from: "https://raw.githubusercontent.com/UnRealDinnerbone/GradleHelpers/master/version.gradle"
apply from: "https://unreal.codes/2021/3/29/U5UCB1617036408458-testchangelog.gradle"

archivesBaseName = project.archives_base_name
group = project.maven_group
version = getVersionNumber()

loom {
    silentMojangMappingsLicense()
}

repositories {
    maven {
        url = "https://maven.architectury.dev/"
    }
}

dependencies {
    minecraft "com.mojang:minecraft:${project.minecraft_version}"
    mappings minecraft.officialMojangMappings()
    forge "net.minecraftforge:forge:${project.minecraft_version}-${project.forge_version}"
}

processResources {
    inputs.property "version", project.version
    filesMatching("META-INF/mods.toml") {
        expand "version": project.version
    }
}

tasks.withType(JavaCompile).configureEach {
    it.options.encoding = "UTF-8"
    def targetVersion = 8
    if (JavaVersion.current().isJava9Compatible()) {
        it.options.release = targetVersion
    }
}

jar {
    manifest {
        attributes([
                "Specification-Title"     : project.mod_id,
                "Specification-Vendor"    : project.mod_author,
                "Specification-Version"   : "1",
                "Implementation-Title"    : project.name,
                "Implementation-Version"  : version,
                "Implementation-Vendor"   : project.mod_author,
                "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
        ])
    }
}

curseforge {
    if (System.getenv().get('unreal_curse_api_key')){
        apiKey = System.getenv().get('unreal_curse_api_key')
    }
    project {
        id = project.property("curseforge_id")
        changelog = getGitChangelog()
        changelogType = 'markdown'
        releaseType = project.property("type")
        addGameVersion minecraft_version
        addGameVersion 'Forge'
        mainArtifact jar
    }
}
